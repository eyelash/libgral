cmake_minimum_required(VERSION 3.0)
project(libgral)

if(NOT WIN32 AND NOT APPLE)
	set(LINUX TRUE)
endif()

if(WIN32)
	find_library(GDIPLUS gdiplus)
	find_library(WINMM WinMM)
	add_library(gral gral_windows.cpp)
	target_link_libraries(gral ${GDIPLUS} ${WINMM})
elseif(APPLE)
	find_library(COCOA Cocoa)
	find_library(AUDIO_TOOLBOX AudioToolbox)
	add_library(gral gral_macos.m)
	target_link_libraries(gral ${COCOA} ${AUDIO_TOOLBOX})
elseif(LINUX)
	find_package(PkgConfig REQUIRED)
	pkg_check_modules(GTK REQUIRED gtk+-3.0)
	find_package(ALSA REQUIRED)
	add_library(gral gral_linux.c)
	target_include_directories(gral PRIVATE ${GTK_INCLUDE_DIRS} ${ALSA_INCLUDE_DIRS})
	target_compile_options(gral PRIVATE ${GTK_CFLAGS_OTHER})
	target_link_libraries(gral ${GTK_LIBRARIES} ${ALSA_LIBRARIES})
endif()

option(BUILD_DEMO "build the demo application")
if(BUILD_DEMO)
	add_executable(demo demo.c)
	target_include_directories(demo PRIVATE ${PROJECT_SOURCE_DIR})
	target_link_libraries(demo gral)
	if(LINUX)
		target_link_libraries(demo m)
	endif()
endif()
