cmake_minimum_required(VERSION 3.0)
project(libgral)

if(CMAKE_SYSTEM_NAME STREQUAL "Windows")
	add_library(gral gral_windows.cpp)
	target_link_libraries(gral gdiplus)
	target_compile_definitions(gral PUBLIC GRAL_WINDOWS)
elseif(CMAKE_SYSTEM_NAME STREQUAL "Darwin")
	find_library(COCOA Cocoa)
	find_library(AUDIO_TOOLBOX AudioToolbox)
	add_library(gral gral_macos.m)
	target_link_libraries(gral ${COCOA} ${AUDIO_TOOLBOX})
	target_compile_definitions(gral PUBLIC GRAL_MACOS)
elseif(CMAKE_SYSTEM_NAME STREQUAL "Linux")
	find_package(PkgConfig REQUIRED)
	pkg_check_modules(GTK REQUIRED gtk+-3.0)
	pkg_check_modules(ALSA REQUIRED alsa)
	add_library(gral gral_linux.c)
	target_include_directories(gral PRIVATE ${GTK_INCLUDE_DIRS} ${ALSA_INCLUDE_DIRS})
	target_compile_options(gral PRIVATE ${GTK_CFLAGS_OTHER} ${ALSA_CFLAGS_OTHER})
	target_link_libraries(gral ${GTK_LIBRARIES} ${ALSA_LIBRARIES} m)
	target_compile_definitions(gral PUBLIC GRAL_LINUX)
endif()
target_include_directories(gral PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})

option(BUILD_DEMOS "build the demo applications")
if(BUILD_DEMOS)
	add_subdirectory(demos)
endif()
